apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 'Google Inc.:Google APIs:23'
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "me.passy.photoshare"
        minSdkVersion 23
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    signingConfigs {
        debug {
            storeFile file("../config/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }
   sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    // Allows us to use a gradles.properties to specify these instead of having
    // to hardcode them in Java and check them in into VCS.
    applicationVariants.all { variant ->
        variant.buildConfigField 'String', 'PARSE_APPLICATION_ID', "\"" + parseApplicationId + "\""
        variant.buildConfigField 'String', 'PARSE_CLIENT_KEY', "\"" + parseClientKey + "\""
        variant.buildConfigField 'String', 'TWITTER_CONSUMER_KEY', "\"" + twitterConsumerKey + "\""
        variant.buildConfigField 'String', 'TWITTER_CONSUMER_SECRET', "\"" + twitterConsumerSecret + "\""
    }
}

repositories {
    jcenter()
    maven { url 'https://github.com/8tory/parse-android-sdk.m2/raw/master/' }
    mavenCentral()

    // Currently only used for rxbindings-kotlin
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

kapt {
    generateStubs = true
}

dependencies {
    // JARs
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Android
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:multidex:1.0.1'
    // Parse
    compile 'com.parse:parse-android:1.13.0'
    compile 'com.parse:parseui-login-android:0.0.1'
    compile 'com.parse:parseui-widget-android:0.0.1'
    compile 'com.parse:parsetwitterutils-android:1.10.4'
    // Kotlin
    compile 'org.jetbrains.anko:anko-sdk23:0.8.2'
    // Rx
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'com.jakewharton.rxbinding:rxbinding-kotlin:0.3.1-SNAPSHOT'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'io.reactivex:rxkotlin:0.40.1'
    // Dagger 2
    compile 'com.google.dagger:dagger:2.0.2'
    kapt 'com.google.dagger:dagger-compiler:2.0.2'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile('com.infstory:rxparse:1.0.3') {
        exclude module: 'parse'
        exclude module: 'bolts-android'
    }
    compile 'com.trello:rxlifecycle:0.3.0'
    compile 'com.trello:rxlifecycle-components:0.3.0'
    compile 'io.reactivex:rxjava-debug:1.0.2'
    compile 'com.squareup.okhttp:okhttp:2.7.2'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.7.2'

    // Various
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-beta2'
    compile 'com.jakewharton:butterknife:7.0.1'
    kapt 'com.jakewharton:butterknife:7.0.1'

    // Testing
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:1.7.1'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'com.googlecode.catch-exception:catch-exception:1.2.0'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
buildscript {
    ext.kotlin_version = '1.0.0-rc-1036'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
